plugins {
    id 'java-library'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'org.spongepowered.gradle.vanilla' version '0.2.1-SNAPSHOT'
    id 'com.diffplug.spotless' version '6.25.0'
}

allprojects {
    apply plugin: 'java-library'

    group 'net.smoofyuniverse'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = "utf-8"
    }

    repositories {
        mavenCentral()
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }
    }

    configurations {
        provided
        shade

        provided.extendsFrom shade
        implementation.extendsFrom provided
    }

    jar {
        archiveClassifier = 'base'
    }
}

subprojects {
    apply plugin: 'com.github.johnrengelman.shadow'

    shadowJar {
        archiveClassifier = 'shadow'
        configurations = [project.configurations.shade]

        dependencies {
            include project(':')
            include dependency('net.smoofyuniverse:oreapi')
            include dependency('net.smoofyuniverse:oreupdater')
            include dependency('net.smoofyuniverse:worldmap')
            include dependency('net.smoofyuniverse:bingo')
        }

        relocate 'net.smoofyuniverse.ore', 'net.smoofyuniverse.mirage.ore'
        relocate 'net.smoofyuniverse.map', 'net.smoofyuniverse.mirage.map'
        relocate 'net.smoofyuniverse.bingo', 'net.smoofyuniverse.mirage.bingo'

        manifest {
            attributes('MixinConfigs': 'mixins.mirage.json')
        }
    }

    dependencies {
        provided project(path: ':', configuration: 'provided')
        shade project(path: ':', transitive: false)
        shade project(path: ':', configuration: 'shade')
    }
}

dependencies {
    provided 'org.spongepowered:sponge:1.19.4-10.0.0-SNAPSHOT:dev'
    provided 'org.spongepowered:mixin:0.8.5'
    provided 'org.ow2.asm:asm-util:9.7'

    shade 'net.smoofyuniverse:oreupdater:1.2.0'
    shade 'net.smoofyuniverse:worldmap:1.2.0'
    shade 'net.smoofyuniverse:bingo:1.0.1'
}

minecraft {
    version '1.19.4'
    accessWideners(file("src/main/resources/mirage.accesswidener"))
}

spotless {
    ratchetFrom 'origin/master'

    java {
        licenseHeaderFile(rootProject.file('HEADER'))
    }
}
