/*
 * Copyright (c) 2018-2022 Hugo Dupanloup (Yeregorix)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

plugins {
    id 'java-library'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'org.spongepowered.gradle.vanilla' version '0.2'
}

group 'net.smoofyuniverse'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "utf-8"
}

repositories {
    mavenCentral()
    maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/repository/maven-public/'
    }
}

configurations {
    spongevanilla
    spongeforge
}

dependencies {
    implementation 'org.spongepowered:sponge:1.16.5-8.1.0-SNAPSHOT:dev'
    implementation 'org.spongepowered:mixin:0.8.4'
    implementation 'org.ow2.asm:asm-util:9.2'

    implementation 'net.smoofyuniverse:oreupdater:1.1.1'
    implementation 'net.smoofyuniverse:worldmap:1.1.0'
    implementation 'net.smoofyuniverse:bingo:1.0.1'

    spongevanilla 'org.spongepowered:spongevanilla:1.16.5-8.1.0-RC1185:universal'
    spongeforge 'org.spongepowered:spongeforge:1.16.5-36.2.5-8.1.0-RC1185:universal'
}

minecraft {
    version("1.16.5")

    project.sourceSets.main.resources
            .filter { it.name.endsWith(".accesswidener") }
            .files.forEach { accessWideners(it) }
}

jar {
    classifier 'base'
    manifest {
        attributes(
                'MixinConfigs': 'mixins.mirage.json',
                'Access-Widener': 'mirage.accesswidener'
        )
    }
}

shadowJar {
    classifier ''

    dependencies {
        include dependency('net.smoofyuniverse:oreapi')
        include dependency('net.smoofyuniverse:oreupdater')
        include dependency('net.smoofyuniverse:worldmap')
        include dependency('net.smoofyuniverse:bingo')
    }

    relocate 'net.smoofyuniverse.ore', 'net.smoofyuniverse.mirage.ore'
    relocate 'net.smoofyuniverse.map', 'net.smoofyuniverse.mirage.map'
    relocate 'net.smoofyuniverse.bingo', 'net.smoofyuniverse.mirage.bingo'
}

task setupVanillaServer(type: Copy) {
    into 'run/vanilla'

    from configurations.spongevanilla
    rename('spongevanilla-(.*).jar', 'spongevanilla.jar')

    into('mods') {
        from shadowJar
    }
}

task setupForgeServer(type: Copy) {
    into 'run/forge/mods'

    from configurations.spongeforge
    rename('spongeforge-(.*).jar', 'spongeforge.jar')

    from shadowJar
}
