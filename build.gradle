/*
 * Copyright (c) 2018-2022 Hugo Dupanloup (Yeregorix)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

plugins {
    id 'java-library'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'org.spongepowered.gradle.vanilla' version '0.2.1-SNAPSHOT'
}

allprojects {
    apply plugin: 'java-library'

    group 'net.smoofyuniverse'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(8)
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = "utf-8"
    }

    repositories {
        mavenCentral()
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }
    }

    configurations {
        provided
        shade

        provided.extendsFrom shade
        implementation.extendsFrom provided
    }

    jar {
        classifier 'base'
    }
}

subprojects {
    apply plugin: 'com.github.johnrengelman.shadow'

    shadowJar {
        classifier 'shadow'
        configurations = [project.configurations.shade]

        dependencies {
            include project(':')
            include dependency('net.smoofyuniverse:oreapi')
            include dependency('net.smoofyuniverse:oreupdater')
            include dependency('net.smoofyuniverse:worldmap')
            include dependency('net.smoofyuniverse:bingo')
        }

        relocate 'net.smoofyuniverse.ore', 'net.smoofyuniverse.mirage.ore'
        relocate 'net.smoofyuniverse.map', 'net.smoofyuniverse.mirage.map'
        relocate 'net.smoofyuniverse.bingo', 'net.smoofyuniverse.mirage.bingo'

        manifest {
            attributes('MixinConfigs': 'mixins.mirage.json')
        }
    }

    dependencies {
        provided project(path: ':', configuration: 'provided')
        shade project(path: ':', transitive: false)
        shade project(path: ':', configuration: 'shade')
    }
}

dependencies {
    provided 'org.spongepowered:sponge:1.16.5-8.1.0-SNAPSHOT:dev'
    provided 'org.spongepowered:mixin:0.8.4'
    provided 'org.ow2.asm:asm-util:9.2'

    shade 'net.smoofyuniverse:oreupdater:1.1.1'
    shade 'net.smoofyuniverse:worldmap:1.1.0'
    shade 'net.smoofyuniverse:bingo:1.0.1'
}

minecraft {
    version '1.16.5'
    accessWideners(file("src/main/resources/mirage.accesswidener"))
}
