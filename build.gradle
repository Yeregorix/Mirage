/*
 * Copyright (c) 2018-2020 Hugo Dupanloup (Yeregorix)
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

buildscript {
    repositories {
        maven {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }
        maven {
            name = 'sponge'
            url = 'https://repo-new.spongepowered.org/repository/maven-public'
        }
    }

    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
    }
}

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '4.0.4'
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'

sourceCompatibility = 1.8
targetCompatibility = 1.8

minecraft {
    version = '1.12.2-14.23.5.2838'
    mappings = 'snapshot_20180808'

    runDir = 'run'
    makeObfSourceJar = false
}

sourceSets {
    main {
        ext.refMap = 'mixins.mirage.refmap.json'
    }
}

repositories {
    mavenCentral()
    maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/repository/maven-public/'
    }
}

dependencies {
    compile 'org.spongepowered:spongecommon:1.12.2-7.3.0:dev'
    compile 'net.smoofyuniverse:oreupdater:1.0.1'
    compile 'net.smoofyuniverse:bingo:1.0.1'
}

jar {
    classifier 'base'
    manifest {
        attributes(
                'FMLCorePluginContainsFMLMod': 'true',
                'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
                'MixinConfigs': 'mixins.mirage.json',
                'FMLAT': 'mirage_at.cfg',
                'ForceLoadAsMod' : true
        )
    }
}

reobf.jar.mappingType = 'SEARGE'

shadowJar {
    classifier ''
    mainSpec.sourcePaths.clear()
    dependsOn reobfJar

    dependencies {
        include dependency('net.smoofyuniverse:oreapi')
        include dependency('net.smoofyuniverse:oreupdater')
        include dependency('net.smoofyuniverse:bingo')
    }

    relocate 'net.smoofyuniverse.ore', 'net.smoofyuniverse.mirage.ore'
    relocate 'net.smoofyuniverse.bingo', 'net.smoofyuniverse.mirage.bingo'

    exclude "dummyThing"
    afterEvaluate {
        from zipTree(reobfJar.jar)
    }
}

task updatePlugin <<{
    copy {
        from 'build/libs/Mirage.jar'
        into 'run/forge/mods'
    }
    copy {
        from 'build/libs/Mirage.jar'
        into 'run/vanilla/mods'
    }
}
updatePlugin.dependsOn shadowJar