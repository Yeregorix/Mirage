plugins {
    id "dev.architectury.loom" version "1.5-SNAPSHOT"
}

archivesBaseName = 'Mirage-forge'

loom {
    silentMojangMappingsLicense()
    accessWidenerPath = file('../src/main/resources/mirage.accesswidener')

    forge {
        convertAccessWideners = true
    }

    mixin {
        useLegacyMixinAp = false
    }
}

configurations {
    spongeforge
}

dependencies {
    minecraft 'com.mojang:minecraft:1.19.4'
    forge 'net.minecraftforge:forge:1.19.4-45.1.19'
    mappings loom.officialMojangMappings()

    spongeforge('org.spongepowered:spongeforge:1.19.4-45.1.19-10.0.0-RC1552:universal') { transitive = false }
}

remapJar {
    inputFile.set shadowJar.archiveFile
    dependsOn shadowJar
}

build.dependsOn remapJar

tasks.register('setupServer', Copy) {
    into 'run/mods'

    from configurations.spongeforge
    rename('spongeforge-(.*).jar', 'spongeforge.jar')

    from remapJar
}
